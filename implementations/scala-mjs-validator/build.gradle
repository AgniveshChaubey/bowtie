plugins {
    id 'java'
}

sourceSets {
    main {
        java {
            srcDir '.'
            include 'Harness.java'
        }
    }
}

repositories {
    mavenCentral()
}

def harnessImplementation = [
        group: 'org.up.js',
        name: 'mjs',
        version: '1.0'
]
def jsonProvider = [
        group: 'com.fasterxml.jackson.core',
        name: 'jackson-databind',
        version: '2.16.0'
]

dependencies {
    implementation files('mjs.jar')
    /* Verbose dependency notation for dependabot */
    implementation "$jsonProvider.group:$jsonProvider.name:$jsonProvider.version"
}

jar {
    archiveBaseName = 'harness'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes "Main-Class": "Harness"
        attributes "Implementation-Group": harnessImplementation.group
        attributes "Implementation-Name": harnessImplementation.name
        attributes "Implementation-Version": harnessImplementation.version
        attributes "Provider-Group": jsonProvider.group
        attributes "Provider-Name": jsonProvider.name
        attributes "Provider-Version": jsonProvider.version
    }
}
